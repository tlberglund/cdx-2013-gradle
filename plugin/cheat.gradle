
import org.gradle.api.DefaultTask
import org.gradle.api.tasks.TaskAction
import liquibase.integration.commandline.Main

buildscript { 
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'org.liquibase:liquibase-core:2.0.1'
    classpath 'com.h2database:h2:1.3.153'
  }
}

task generateChangeLog(type: LiquibaseTask) {
  url = 'jdbc:h2:db/gradle_example_database;FILE_LOCK=NO'
  username = 'sa'
  password = ''
  changeLogFile = file('changelog.xml')
  command = 'generateChangeLog'
}


class LiquibaseTask extends DefaultTask { 
  def url
  def username
  def password
  File changeLogFile
  def command

  @TaskAction
  def liquibaseAction() { 
    def args = [ "--url=${url}", "--username=${username}",
                 "--password=${password}",
                 "--changeLogFile=${changeLogFile.absolutePath}",
                 command]

    Main.main(args as String[])
  }
} 